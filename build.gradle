plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.10'
    id 'org.beryx.jlink' version '2.24.4'
    id 'com.coditory.manifest' version '0.1.14'

    // TODO: remove this once it's released into the application plugin:
    id 'org.javamodularity.moduleplugin' version '1.8.10' apply false
}

version="2.2.1"

group = 'nu.mine.mosher.gedcom'

java {
    sourceCompatibility = JavaVersion.VERSION_15
    modularity.inferModulePath.set true
}
compileJava {
    options.compilerArgs << '-parameters'
//    options.compilerArgs << "--enable-preview"
}

//tasks.withType(JavaExec) {
//    jvmArgs += "--enable-preview"
//}

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'nu.mine.mosher.gedcom', name: 'gedcom-lib', version: 'latest.release', {
        exclude group: 'nu.mine.mosher.mopper', module: 'mopper'
    }
    implementation group: 'org.xerial', name: 'sqlite-jdbc', version: 'latest.release'

    implementation group: 'nu.mine.mosher.io', name: 'log-files', version: 'latest.release'

    implementation group: 'org.slf4j', name: 'jcl-over-slf4j', version: 'latest.integration'
    implementation group: 'org.slf4j', name: 'log4j-over-slf4j', version: 'latest.integration'
    implementation group: 'org.slf4j', name: 'jul-to-slf4j', version: 'latest.integration'
    implementation group: 'org.slf4j', name: 'slf4j-api', version: 'latest.integration'
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: 'latest.release'
}

application {
    getMainModule().set 'nu.mine.mosher.gedcom.xy'
    getMainClass().set 'nu.mine.mosher.gedcom.xy.GenXyEditor'
}

javafx {
    modules = [
        'javafx.controls',
        'javafx.swing'
    ]
}

jar {
    //noinspection GroovyAssignabilityCheck
    manifest {
        attributes(
            'Specification-Version': manifest.attributes.get('Implementation-Version'),
            'Implementation-Version': manifest.attributes.get('Implementation-Version').toString()+"+"+manifest.attributes.get('SCM-Commit-Hash').toString()
        )
    }
}

def os = org.gradle.internal.os.OperatingSystem.current()

jlink {
    mergedModule {
        additive = true
        requires 'org.slf4j'
    }
    jpackage {
        if (os.windows) {
            installerOptions += [
                '--win-dir-chooser',
                '--win-menu',
                '--win-shortcut'
            ]
        } else if (os.linux) {
            installerOptions += [
                '--linux-shortcut'
            ]
        }
        installerOptions += [ '--verbose' ]
    }
}

assemble {
    dependsOn tasks.jpackage
}
