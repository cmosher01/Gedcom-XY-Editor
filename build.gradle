import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'org.beryx.jlink' version '2.25.0'
    id 'com.coditory.manifest' version '0.2.0'
}

version = '2.2.3+'+jar.manifest.attributes.get('SCM-Commit-Hash')

group = 'nu.mine.mosher.gedcom'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of 15
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'nu.mine.mosher.gedcom', name: 'gedcom-lib', version: 'latest.release', {
        exclude group: 'nu.mine.mosher.mopper', module: 'mopper'
    }
    implementation group: 'org.xerial', name: 'sqlite-jdbc', version: 'latest.release'
    implementation group: 'com.itextpdf', name: 'itext7-core', version: 'latest.release'

    implementation group: 'nu.mine.mosher.io', name: 'log-files', version: 'latest.release'

    implementation group: 'org.slf4j', name: 'jcl-over-slf4j', version: 'latest.integration'
    implementation group: 'org.slf4j', name: 'log4j-over-slf4j', version: 'latest.integration'
    implementation group: 'org.slf4j', name: 'jul-to-slf4j', version: 'latest.integration'
    implementation group: 'org.slf4j', name: 'slf4j-api', version: 'latest.integration'
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: 'latest.release'
}

application {
    mainModule = 'nu.mine.mosher.gedcom.xy'
    mainClass = 'nu.mine.mosher.gedcom.xy.GenXyEditor'
}

javafx {
    modules = [
        'javafx.controls',
        'javafx.swing'
    ]
}

jlink {
    mergedModule {
        additive = true
        requires 'org.slf4j'
    }
    jpackage {
        if (DefaultNativePlatform.currentOperatingSystem.windows) {
            installerOptions += [ '--win-dir-chooser', '--win-menu', '--win-shortcut' ]
        } else if (DefaultNativePlatform.currentOperatingSystem.linux) {
            installerOptions += [ '--linux-shortcut' ]
        }
        installerOptions += [ '--verbose' ]
    }
}

tasks.assemble.dependsOn tasks.jpackage
